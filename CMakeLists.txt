cmake_minimum_required(VERSION 3.23)
project(coro-http)

option(BUILD_SHARED_LIBS "build shared libs" OFF)
option(BUILD_TESTING "build testing" OFF)
option(BUILD_EXAMPLES "build examples" ON)
option(WITH_STACKTRACE "enable stacktraces in exceptions" OFF)
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

find_package(CURL 7.77.0 REQUIRED)
find_package(Libevent 2.1.12 REQUIRED)
find_package(Boost 1.76.0 REQUIRED COMPONENTS regex)

if(WITH_STACKTRACE)
    find_path(BOOST_STACKTRACE boost/stacktrace.hpp REQUIRED)
    if(MSVC)
        add_library(boost_stacktrace INTERFACE)
        target_include_directories(boost_stacktrace INTERFACE ${BOOST_STACKTRACE})
    else()
        find_library(BACKTRACE_LIBRARY backtrace REQUIRED)
        add_library(boost_stacktrace INTERFACE)
        target_include_directories(boost_stacktrace INTERFACE ${BOOST_STACKTRACE})
        target_link_libraries(boost_stacktrace INTERFACE ${BACKTRACE_LIBRARY} ${CMAKE_DL_LIBS})
        target_compile_definitions(boost_stacktrace INTERFACE BOOST_STACKTRACE_USE_BACKTRACE)
    endif()
    add_library(Boost::stacktrace ALIAS boost_stacktrace)
endif()

add_subdirectory(src)
if(BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()

if(BUILD_TESTING)
    enable_testing()
    add_subdirectory(test)
endif()
